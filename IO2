 T 55 K         [55 -  начальная ячейка]
 GK             [@ = 55]
                [ВХОДНЫЕ ДАННЫЕ]
 [@+0]P 28 D    [Указатель на 1 элемент массива] [55]
 [@+1]P 3 F     [Длина массива]                  [56]
 [@+2]P 6 F     [1-й элем = 12]                  [57]
 [@+3]P 18 F    [2-й элем = 36]                  [58]
 [@+4]P 55 F    [3-й элем = 110]                 [59]
 [@+5]P 0 D     [4-й элем = 1]                   [60]
 [@+6]P 3 F     [5-й элем = 6]                   [61]
 [@+7]P 49 D    [6-й элем = 99]                  [62]
 [@+8]T 0 F     [Команда чтения из acc]          [63]
 [@+9]A 0 F     [Команда записи в acc]           [64]
 [@+10]P 0 D    [1]                              [65]
 [@+11]P 0 D    [i]                              [66]
 [@+12]P 0 F    [j]                              [67]
 [@+13]P 2 F    [k-й элемент (сейчас 4-й)]       [68]

                [Подпрограмма]
 T 100 K        [100 - начальная ячейка]
 GK             [@ = 100]
 [@+0]A 3 F     [Формирование инструкции возврата в acc]
 [@+1]T 58 @    [Указатель на точку выхода из подпрограммы]
                [Цикл i]
 [@+2]T 0 F     [Обнуление acc]
 [@+3]A 66 F    [Берём i]
 [@+4]A 55 F    [Добавляем указатель на 1 элемент массива]
 [@+5]L 0 D     [Сдвиг acc на 1 разряд влево]
 [@+6]X 0 F
 [@+7]A 64 F    [Добавляем команду A 0 F из ячейки 64]
 [@+8]T 9 @     [Записываем результат в ячейку 9 подпрограммы, обнуляем acc]
 [@+9]A 0 F     [Прочитали i-й элемент]
 [@+10]T 1 F    [Положили его в рабочую ячейку 1 - insertElement]
 [@+11]A 66 F   [Берём i]
 [@+12]S 65 F   [-1]
 [@+13]U 67 F   [Запоминаем i-1 в j]
                [ЦИКЛ j]
 [@+14]A 55 F   [Берём указатель на 1 элемент массива]
 [@+15]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+16]A 64 F   [Добавляем команду A 0 F]
 [@+17]T 18 @   [Записываем результат в ячейку 18 подпрограммы, обнуляем acc]
 [@+18]A 0 F    [Прочитали list(j)]
 [@+19]U 4 F    [Запомнили list(j) в рабочую ячейку 4]
 [@+20]S 1 F    [Сравниваем с list(i)]
 [@+21]G 35 @   [Если acc < 0 значит list(j) < list(i) следовательно конец цикла]
 [@+22]T 0 F    [Обнуление acc]
 [@+23]A 55 F   [Берём указатель на 1 элемент массива]
 [@+24]A 67 F   [+j]
 [@+25]A 65 F   [+1]
 [@+26]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+27]A 63 F   [Добавляем команду T 0 F]
 [@+28]T 30 @   [Записываем результат в ячейку 30 подпрограммы, обнуляем acc]
 [@+29]A 4 F    [Берём list(j) из рабочей ячейки 4]
 [@+30]T 0 F    [list(j+1) = list(j), обнуляем acc]
 [@+31]A 67 F   [Берём j]
 [@+32]S 65 F   [-1]
 [@+33]U 67 F   [Запомнили j--]
 [@+34]E 14 @   [Вернулись в цикл с j]
                [КОНЕЦ ЦИКЛА j]
 [@+35]T 0 F    [Обнуление acc]
 [@+36]A 67 F   [Берём j]
 [@+37]A 55 F   [Добавляем указатель на 1 элемент массива]
 [@+38]A 65 F   [+1]
 [@+39]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+40]A 63 F   [Добавляем команду T 0 F]
 [@+41]T 43 @   [Записываем результат в ячейку 43 подпрограммы, обнуляем acc]
 [@+42]A 1 F    [Берём insertElement из рабочей ячейки 1]
 [@+43]T 0 F    [list(j+1) = insertElement, обнуляем acc]
 [@+44]A 66 F   [Берём i]
 [@+45]A 65 F   [+1]
 [@+46]U 66 F   [Запоминаем i++]
 [@+47]S 56 F   [i - length]
 [@+48]G 2 @    [Если i < length, то переходим в начало цикла]
                [СОРТИРОВКА ЗАКОНЧЕНА]
 [@+49]T 0 F    [Обнуление acc]
 [@+50]A 55 F   [Берём указатель]
 [@+51]A 68 F   [+k]
 [@+52]S 65 F   [-1]
 [@+53]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+54]A 64 F   [Добавление команды A 0 F]
 [@+55]T 56 @   [Записываем результат в ячейку 56 подпрограммы, обнуляем acc]
 [@+56]A 0 F    [k-й элем отсортированного массива]
 [@+57]T 5 F    [Вывод в рабочую ячейку 5]
 [@+58]Z 0 F    [Остановка машины]

                [Программа]
 T 200 K        [программа начинается с 200 ячейки]
 GK             [@ = 200]
 [@+0]T 0 F     [Обнуление acc]
 [@+1]A 2 @     [Запись в аккумулятор текущего адреса для]
                [формирования в подпрограмме инструкции возврата]
 [@+2]X 0 F
 [@+3]G 100 F   [Вызов подпрограммы]
 [@+4]X 0 F
 [@+5]EZPF      [Переход к в первой инструкции программы]
