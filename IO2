                [ВХОДНЫЕ ДАННЫЕ]
 T 55 K         [55 -  начальная ячейка]
 GK             [@ = 55]
 [@+0]X 0 F     [Будущий указатель на 1 элемент массива] [55]
 [@+1]P 3 F     [Длина массива]                          [56]
 [@+2]X 0 F     [Будудщий k-й элемент]                   [57]
 [@+3]P 0 D     [1]                                      [58]
 [@+4]P 0 D     [i]                                      [59]
 [@+5]P 0 F     [j]                                      [60]
 [@+6]A 0 F     [Команда записи в acc]                   [61]
 [@+7]T 0 F     [Команда чтения из acc]                  [62]

                [МАССИВ]
 T 300 K        [300 - начальная ячейка]
 GK             [@ = 300]
 [@+0]P 6 F     [1-й элем = 12]                          [300]
 [@+1]P 18 F    [2-й элем = 36]                          [301]
 [@+2]P 55 F    [3-й элем = 110]                         [302]
 [@+3]P 0 D     [4-й элем = 1]                           [303]
 [@+4]P 3 F     [5-й элем = 6]                           [304]
 [@+5]P 49 D    [6-й элем = 99]                          [305]

                [Подпрограмма]
 T 100 K        [100 - начальная ячейка]
 GK             [@ = 100]
 [@+0]A 3 F     [Формирование инструкции возврата в acc]
 [@+1]T 58 @    [Указатель на точку выхода из подпрограммы]
                [Цикл i]
 [@+2]T 0 F     [Обнуление acc]
 [@+3]A 59 F    [Берём i]
 [@+4]A 55 F    [Добавляем указатель на 1 элемент массива]
 [@+5]L 0 D     [Сдвиг acc на 1 разряд влево]
 [@+6]X 0 F
 [@+7]A 61 F    [Добавляем команду A 0 F из ячейки 64]
 [@+8]T 9 @     [Записываем результат в ячейку 9 подпрограммы, обнуляем acc]
 [@+9]A 0 F     [Прочитали i-й элемент]
 [@+10]T 1 F    [Положили его в рабочую ячейку 1 - insertElement]
 [@+11]A 59 F   [Берём i]
 [@+12]S 58 F   [-1]
 [@+13]U 60 F   [Запоминаем i-1 в j]
                [ЦИКЛ j]
 [@+14]A 55 F   [Берём указатель на 1 элемент массива]
 [@+15]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+16]A 61 F   [Добавляем команду A 0 F]
 [@+17]T 18 @   [Записываем результат в ячейку 18 подпрограммы, обнуляем acc]
 [@+18]A 0 F    [Прочитали list(j)]
 [@+19]U 4 F    [Запомнили list(j) в рабочую ячейку 4]
 [@+20]S 1 F    [Сравниваем с list(i)]
 [@+21]G 35 @   [Если acc < 0 значит list(j) < list(i) следовательно конец цикла]
 [@+22]T 0 F    [Обнуление acc]
 [@+23]A 55 F   [Берём указатель на 1 элемент массива]
 [@+24]A 60 F   [+j]
 [@+25]A 58 F   [+1]
 [@+26]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+27]A 62 F   [Добавляем команду T 0 F]
 [@+28]T 30 @   [Записываем результат в ячейку 30 подпрограммы, обнуляем acc]
 [@+29]A 4 F    [Берём list(j) из рабочей ячейки 4]
 [@+30]T 0 F    [list(j+1) = list(j), обнуляем acc]
 [@+31]A 60 F   [Берём j]
 [@+32]S 58 F   [-1]
 [@+33]U 60 F   [Запомнили j--]
 [@+34]E 14 @   [Вернулись в цикл с j]
                [КОНЕЦ ЦИКЛА j]
 [@+35]T 0 F    [Обнуление acc]
 [@+36]A 60 F   [Берём j]
 [@+37]A 55 F   [Добавляем указатель на 1 элемент массива]
 [@+38]A 58 F   [+1]
 [@+39]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+40]A 62 F   [Добавляем команду T 0 F]
 [@+41]T 43 @   [Записываем результат в ячейку 43 подпрограммы, обнуляем acc]
 [@+42]A 1 F    [Берём insertElement из рабочей ячейки 1]
 [@+43]T 0 F    [list(j+1) = insertElement, обнуляем acc]
 [@+44]A 59 F   [Берём i]
 [@+45]A 58 F   [+1]
 [@+46]U 59 F   [Запоминаем i++]
 [@+47]S 56 F   [i - length]
 [@+48]G 2 @    [Если i < length, то переходим в начало цикла]
                [СОРТИРОВКА ЗАКОНЧЕНА]
 [@+49]T 0 F    [Обнуление acc]
 [@+50]A 55 F   [Берём указатель]
 [@+51]A 57 F   [+k]
 [@+52]S 58 F   [-1]
 [@+53]L 0 D    [Сдвиг acc на 1 разряд влево]
 [@+54]A 61 F   [Добавление команды A 0 F]
 [@+55]T 56 @   [Записываем результат в ячейку 56 подпрограммы, обнуляем acc]
 [@+56]A 0 F    [k-й элем отсортированного массива]
 [@+57]T 5 F    [Вывод в рабочую ячейку 5]
 [@+58]E 0 F    [Возврат в программу]

                [Программа]
 T 200 K        [программа начинается с 200 ячейки]
 GK             [@ = 200]
 [@+0]T 0 F     [Обнуление acc]
 [@+1]A 8 @     [Берём указатель на 1 элемент данного массива из 8 ячейки этой программы]
 [@+2]T 55 F    [Записываем указатель в 55 ячейку (подпрограмма констант)]
 [@+3]A 9 @     [Берём k-й элемент из 9 ячейки этой программы]
 [@+4]T 57 F    [Записываем k-й элемент в 57 ячейку (подпрограмма констант)]
 [@+5]A 5 @     [Запись в аккумулятор текущего адреса для]
                [формирования в подпрограмме инструкции возврата]
 [@+6]G 100 F   [Вызов подпрограммы]
 [@+7]Z 0 F     [Конец программы]
 [@+8]P 150 F   [Указатель на 1 элемент массива]
 [@+9]P 2 F     [k-й элемент]
 [@+10]EZPF     [Переход к в первой инструкции программы]
