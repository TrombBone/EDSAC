[31]T 111 S [Указатель на последнюю команду]
            [Цикл i]
[32]T 0 S   [Обнуление acc]
[33]A 98 S  [Берём i из ячейки 98 и записываем в acc]
[34]X 0 S
[35]A 102 S [Берём указатель на первый элемент массива из ячейки 102 и записываем в acc]
[36]L 0 L   [Сдвиг аккумулятора на 1 разряд влево]
[37]A 100 S [Записываем команду A 0 S из ячейки 100 в аккумулятор]
[38]T 39 S  [Берём предыдущую команду из аккумулятора и записываем в ячейку 39]
[39]A 0 S   [Прочитали i-й элемент]
[40]T 1 S   [Положили в рабочую ячейку (insertElement)]
[41]A 98 S  [Берём i]
[42]S 101 S [Делаем i--]
[43]U 99 S  [Кладём результат в j]
            [Цикл j]
[44]X 0 S
[45]X 0 S
[46]A 102 S [Берём указатель на первый элемент массива]
[47]L 0 L   [Сдвиг acc на 1 разряд влево]
[48]A 100 S [Добавляем команду A 0 S]
[49]T 50 S  [Записываем результат в ячейку 50]
[50]A 0 S   [Прочитали list(j)]
[51]U 2 S   [Запомнили list(j)]
[52]S 1 S   [Сравнили с insertElement]
[53]G 68 S  [Если acc < 0 значит list(j) < list(i), следовательно идём в конец цикла]
[54]X 0 S
[55]T 0 S   [Обнуление acc]
[56]A 102 S [Берём указатель на первый элемент массива]
[57]A 99 S  [+j]
[58]A 101 S [+1]
[59]L 0 L   [Сдвиг acc на 1 разряд влево]
[60]A 97 S  [Добавляем команду T 0 S]
[61]T 63 S  [Записываем в ячейку 63, обнулем acc]
[62]A 2 S   [Кладём в acc list(j)]
[63]T 0 S   [list(j+1) = list(j), потом обнуляем]
[64]A 99 S  [Берём j]
[65]S 101 S [-1]
[66]U 99 S  [j--]
[67]E 44 S  [Возвращаемся в начало j-цикла]
            [КОНЕЦ ЦИКЛА j]
[68]T 0 S   [Обнуляем acc]
[69]A 99 S  [Берём j]
[70]A 102 S [Добавляем указатель на 1 элемент массива]
[71]A 101 S [+1]
[72]L 0 L   [Сдвиг acc на 1 разряд влево]
[73]A 97 S  [Добавляем команду T 0 S]
[74]T 76 S  [Записываем всё из acc в 76 ячейку, обнуляем acc]
[75]A 1 S   [Берём insertElement]
[76]T 0 S   [list(j+1) = insertElement]
[77]A 98 S  [Берём i]
[78]A 101 S [+1]
[79]U 98 S  [i++]
[80]S 104 S [i - length]
[81]G 32 S  [Если i < length то идём в начало цикла]
            [СОРТИРОВКА ЗАКОНЧЕНА]
[82]T 0 S   [Обнуление acc]
[83]A 102 S [Берём указатель на первый элемент массива]
[84]A 103 S [+k]
[85]S 101 S [-1]
[86]L 0 L   [Сдвиг acc на 1 разряд влево]
[87]A 100 S [Добавляем команду A 0 S]
[88]T 89 S  [Записываем результат в ячейку 89]
[89]A 0 S   [k-й элем отсорт массива]
[90]T 3 S   [Вывод результата в рабочую ячейку 3]
[91]Z 0 S   [Остановка машины]
[92]X 0 S
[93]X 0 S
[94]X 0 S
[95]X 0 S
[96]X 0 S
[97]T 0 S   [Команда для чтения из acc]
[98]P 0 L   [i]
[99]P 0 S   [j]
[100]A 0 S  [Команда для добавления в acc]
[101]P 0 L  [1]
[102]P 52 L [Указатель на первый элем массива]
[103]P 2 S  [k-й элем (сейчас 4-й)]
[104]P 3 S  [Длина массива]
[105]P 6 S  [1-й элем = 12]
[106]P 18 S [2-й элем = 36]
[107]P 55 S [3-й элем = 110]
[108]P 0 L  [4-й элем = 1]
[109]P 3 S [5-й элем = 6]
[110]P 49 L [6-й элем = 99]
